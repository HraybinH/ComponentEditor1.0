<?xml version="1.0" encoding="utf-8"?>
<s:ItemRenderer xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:s="library://ns.adobe.com/flex/spark" 
				xmlns:mx="library://ns.adobe.com/flex/mx" width="200" height="30">
	<fx:Declarations>
		<!-- 将非可视元素（例如服务、值对象）放在此处 -->
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import src.wyy.util.FoucusUIMgr;
			import mx.collections.ArrayCollection;
			import mx.events.FlexEvent;
			
			import spark.events.IndexChangeEvent;
			
			import src.wyy.model.ResourceModel;
			import src.wyy.util.BinderManager;
			import src.wyy.vo.KeyValueVo;
			 /**
			 * domain组
			  */
			private var domainAc:ArrayCollection;
			/**
			 * 资源路径
			 */
			private var urlAc:ArrayCollection;
			
			private var kv:KeyValueVo;
			
			override public function set data(value:Object):void
			{
				super.data = value;
				lb.text = value.key;
				
				var kv:KeyValueVo = value as KeyValueVo;
				
				
				if(kv.type == KeyValueVo.bd_type)
				{
					group1.visible = false;
					group2.visible = true;
					
					if(domainAc == null)
					{
						domainAc = new ArrayCollection(ResourceModel.domainArr);
						domainBox.dataProvider = domainAc;
					}
					if(urlAc == null)
					{
						urlAc = new ArrayCollection();
					}
					
					var arr:Array = kv.value.split("~");// domain~url
					var domainStr:String = arr[0];
					var sourceStr:String = arr[1];
					
					urlAc.removeAll();
					if(domainStr == null || domainStr == "")
					{//如果没有设置资源域
						domainBox.selectedIndex = 0;
						urlAc.source = ResourceModel.inst.getSourceArr(ResourceModel.MAIN);
						urlBox.dataProvider = urlAc;
					}
					else
					{
						domainBox.selectedIndex = ResourceModel.domainArr.indexOf(domainStr);
						urlAc.source = ResourceModel.inst.getSourceArr(domainStr);
						urlBox.dataProvider = urlAc;
						urlBox.selectedIndex = urlAc.source.indexOf(sourceStr);
					}
				}
				else
				{
					input.text = value.value;
					group1.visible = true;
					group2.visible = false;
				}
			}
			
			protected function input_enterHandler(event:FlexEvent):void
			{
				BinderManager.inst.setSingleProperty(FoucusUIMgr.inst.editUI,lb.text,input.text);
			}
			
			/**
			 * 资源路径变化
			 */
			protected function urlBox_changeHandler(event:IndexChangeEvent):void
			{
				BinderManager.inst.setSingleProperty(FoucusUIMgr.inst.editUI,lb.text,domainBox.selectedItem + "~" + urlBox.selectedItem);
				
			}
			
			protected function domainBox_changeHandler(event:IndexChangeEvent):void
			{
				urlAc.source = ResourceModel.inst.getSourceArr(domainBox.selectedItem);
				urlBox.dataProvider = urlAc;
				
			}
			
		]]>
	</fx:Script>
	<s:Label id="lb" width="60">
	</s:Label>
	<s:VGroup id="group1" x="60">
		<s:TextInput id="input" x="60" width="140" enter="input_enterHandler(event)" visible="true">
		</s:TextInput>
	</s:VGroup>
	<s:HGroup id="group2" x="60">
		<s:ComboBox id="domainBox" x="0" width="100" change="domainBox_changeHandler(event)"/>
		<s:ComboBox id="urlBox" x="70" width="100" change="urlBox_changeHandler(event)"/>
	</s:HGroup>
</s:ItemRenderer>
